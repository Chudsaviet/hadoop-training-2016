-----------------------------------------------------------
---------- Hive Homework 01 - Timofei Korostelev ----------
-----------------------------------------------------------

SET hive.stats.autogather=true;

SET hive.enforce.bucketing=true;

ADD JAR ./target/hive01-1.0.jar;

CREATE TEMPORARY FUNCTION parseUserAgent AS 'org.tkorostelev.hive01.ParseUserAgent';

-- Create staging fact table as an external table
DROP TABLE IF EXISTS STG_CLICK_FACT;
CREATE EXTERNAL TABLE STG_CLICK_FACT(
    BID_ID                  VARCHAR(32),
    CLICK_DTTM              VARCHAR(30),
    IPINYOU_ID              VARCHAR(15),
    USER_AGENT              VARCHAR(2000),
    IP                      VARCHAR(16),
    REGION                  BIGINT,
    CITY                    BIGINT,
    AD_EXCHANGE             BIGINT,
    DOMAIN_ID               VARCHAR(100),
    URL                     VARCHAR(2000),
    ANONYMOUS_URL_ID        VARCHAR(2000),
    AD_SLOT_ID              VARCHAR(200),
    AD_SLOT_WIDTH           BIGINT,
    AD_SLOT_HEIGHT          BIGINT,
    AD_SLOT_VISIBILITY      BIGINT,
    AD_SLOT_FORMAT          BIGINT,
    PAYING_PRICE            BIGINT,
    CREATIVE_ID             VARCHAR(32),
    BIDDING_PRICE           BIGINT,
    ADVERTISER_ID           VARCHAR(100),
    USER_TAGS               VARCHAR(100),
    STREAM_ID               BIGINT
)
ROW FORMAT DELIMITED
FIELDS TERMINATED BY '\t'
STORED AS TEXTFILE
location '/dataset'
;

-- Create view to do the magic with no copying
DROP VIEW IF EXISTS CLICK_FACT;
CREATE VIEW CLICK_FACT AS 
SELECT
    BID_ID,
    CAST(to_date(from_unixtime(unix_timestamp(CLICK_DTTM,'yyyyMMddHHmmssSSS'))) AS DATE) AS CLICK_DT,
    CAST(from_unixtime(unix_timestamp(CLICK_DTTM,'yyyyMMddHHmmssSSS')) AS TIMESTAMP) AS CLICK_DTTM,
    IPINYOU_ID,
    USER_AGENT,
    IP,
    REGION,
    CITY,
    AD_EXCHANGE,
    DOMAIN_ID,
    URL,
    ANONYMOUS_URL_ID,
    AD_SLOT_ID,
    AD_SLOT_WIDTH,
    AD_SLOT_HEIGHT,
    AD_SLOT_VISIBILITY,
    AD_SLOT_FORMAT,
    PAYING_PRICE,
    CREATIVE_ID,
    BIDDING_PRICE,
    ADVERTISER_ID,
    USER_TAGS,
    STREAM_ID,
    INPUT__FILE__NAME AS INPUT_FILE_NAME,
    SPLIT(parseUserAgent(USER_AGENT),'\\|') AS PARSED_USER_AGENT
FROM
    STG_CLICK_FACT
WHERE
    IPINYOU_ID IS NOT NULL
    AND USER_AGENT IS NOT NULL
    AND LENGTH(IPINYOU_ID) IN (14,15)
    AND CLICK_DTTM IS NOT NULL
;

-- Calculate Bid Flow (point 01) report
DROP TABLE IF EXISTS BID_FLOW PURGE;
CREATE TABLE BID_FLOW
STORED AS ORC tblproperties ("orc.compress"="SNAPPY")
AS SELECT
    CLICK_DT,
    SUM(BIDDING_PRICE) AS BIDDING_PRICE,
    SUM(PAYING_PRICE)  AS PAYING_PRICE
FROM CLICK_FACT
WHERE
    STREAM_ID = 1
GROUP BY CLICK_DT
ORDER BY CLICK_DT
;

-- Calculate User-Date-Clicks table
DROP TABLE IF EXISTS USER_DATE_CLICKS PURGE;
CREATE TABLE USER_DATE_CLICKS
-- CLUSTERED BY (IPINYOU_ID) INTO 32 BUCKETS
STORED AS ORC tblproperties ("orc.compress"="SNAPPY")
AS SELECT
    IPINYOU_ID,
    CAST(CASE
            WHEN
                PARSED_USER_AGENT[2] = 'MOBILE'
            THEN 1 -- Mobile
            WHEN
                PARSED_USER_AGENT[2] = 'TABLET'
            THEN 2 -- Tablet
            WHEN
                PARSED_USER_AGENT[2] = 'BOT'
            THEN 3 -- Bot
            ELSE 0 -- Desktop/Other
        END AS TINYINT ) AS DEVICE,
    CLICK_DT,
    CAST(COUNT(1) AS SMALLINT) AS CLICKS
FROM
    CLICK_FACT
WHERE
    CLICK_DT IS NOT NULL
    AND STREAM_ID = 1
GROUP BY
    IPINYOU_ID,
    CAST(CASE
            WHEN
                PARSED_USER_AGENT[2] = 'MOBILE'
            THEN 1 -- Mobile
            WHEN
                PARSED_USER_AGENT[2] = 'TABLET'
            THEN 2 -- Tablet
            WHEN
                PARSED_USER_AGENT[2] = 'BOT'
            THEN 3 -- Bot
            ELSE 0 -- Desktop/Other
        END AS TINYINT ),
    CLICK_DT
;

CREATE INDEX USER_DATE_CLICKS_INDEX ON TABLE USER_DATE_CLICKS (IPINYOU_ID,DEVICE,CLICK_DT) AS 'COMPACT' WITH DEFERRED REBUILD;
ALTER INDEX USER_DATE_CLICKS_INDEX ON USER_DATE_CLICKS REBUILD;

-- Pull all dates from USER_DATE_CLICKS
DROP TABLE IF EXISTS ALL_DATES PURGE;
CREATE TABLE ALL_DATES
STORED AS ORC tblproperties ("orc.compress"="SNAPPY")
AS SELECT
    CLICK_DT AS DATE_VAL
FROM
    USER_DATE_CLICKS
WHERE
    CLICK_DT IS NOT NULL
GROUP BY
    CLICK_DT
;

-- Pull all users from USER_DATE_CLICKS
DROP TABLE IF EXISTS ALL_USERS PURGE;
CREATE TABLE ALL_USERS
-- CLUSTERED BY (IPINYOU_ID) INTO 32 BUCKETS
STORED AS ORC tblproperties ("orc.compress"="SNAPPY")
AS SELECT
    IPINYOU_ID
FROM
    USER_DATE_CLICKS
GROUP BY
    IPINYOU_ID
;

-- Pull all devices from USER_DATE_CLICKS
DROP TABLE IF EXISTS ALL_DEVICES PURGE;
CREATE TABLE ALL_DEVICES
STORED AS ORC tblproperties ("orc.compress"="SNAPPY")
AS SELECT
    DEVICE
FROM
    USER_DATE_CLICKS
GROUP BY
    DEVICE
;

-- Create ALL_DEVICES_ALL_DATES view as Decart product
DROP TABLE IF EXISTS ALL_DEVICES_ALL_DATES PURGE;
CREATE TABLE ALL_DEVICES_ALL_DATES
STORED AS ORC tblproperties ("orc.compress"="SNAPPY")
AS SELECT
    dev.DEVICE,
    dt.DATE_VAL
FROM
    ALL_DEVICES AS dev
    INNER JOIN
    ALL_DATES AS dt
;
DROP TABLE ALL_DEVICES PURGE;
DROP TABLE ALL_DATES PURGE;

-- Create ALL_USERS_ALL_DEVICES_ALL_DATES view as Decart product
DROP TABLE IF EXISTS ALL_USERS_ALL_DEVICES_ALL_DATES PURGE;
CREATE TABLE ALL_USERS_ALL_DEVICES_ALL_DATES
-- CLUSTERED BY (IPINYOU_ID) INTO 32 BUCKETS
STORED AS ORC tblproperties ("orc.compress"="SNAPPY")
AS SELECT
    u.IPINYOU_ID,
    dd.DEVICE,
    dd.DATE_VAL
FROM
    ALL_USERS AS u
    INNER JOIN
    ALL_DEVICES_ALL_DATES AS dd
;
DROP TABLE ALL_USERS PURGE;
DROP TABLE ALL_DEVICES_ALL_DATES PURGE;

CREATE INDEX ALL_USERS_ALL_DEVICES_ALL_DATES_INDEX ON TABLE ALL_USERS_ALL_DEVICES_ALL_DATES (IPINYOU_ID, DEVICE, DATE_VAL) AS 'COMPACT' WITH DEFERRED REBUILD;
ALTER INDEX ALL_USERS_ALL_DEVICES_ALL_DATES_INDEX ON ALL_USERS_ALL_DEVICES_ALL_DATES REBUILD;

ANALYZE TABLE ALL_USERS_ALL_DEVICES_ALL_DATES COMPUTE STATISTICS;
ANALYZE TABLE ALL_USERS_ALL_DEVICES_ALL_DATES COMPUTE STATISTICS FOR COLUMNS;

-- Create User-AllDates-Clicks view                                         
DROP TABLE IF EXISTS USER_ALLDATES_ALLDEVICES_CLICKS PURGE;
CREATE TABLE USER_ALLDATES_ALLDEVICES_CLICKS
-- CLUSTERED BY (IPINYOU_ID) INTO 32 BUCKETS
STORED AS ORC tblproperties ("orc.compress"="SNAPPY")
AS SELECT
    aaa.IPINYOU_ID,
    aaa.DEVICE,
    aaa.DATE_VAL AS CLICK_DT,
    CAST(COALESCE(udc.CLICKS, 0) AS SMALLINT) AS CLICKS
FROM
    ALL_USERS_ALL_DEVICES_ALL_DATES AS aaa
    LEFT JOIN
    USER_DATE_CLICKS AS udc
    ON (
        aaa.IPINYOU_ID = udc.IPINYOU_ID
        AND aaa.DEVICE = udc.DEVICE
        AND aaa.DATE_VAL = udc.CLICK_DT
    )
;
DROP TABLE ALL_USERS_ALL_DEVICES_ALL_DATES PURGE;
DROP TABLE USER_DATE_CLICKS PURGE;

-- Calculate User-Date-SEGMENT table
DROP TABLE IF EXISTS USER_DEVICE_DAY_SEGMENT PURGE;
CREATE TABLE USER_DEVICE_DAY_SEGMENT
-- CLUSTERED BY (IPINYOU_ID) INTO 32 BUCKETS
STORED AS ORC tblproperties ("orc.compress"="SNAPPY")
AS SELECT
    IPINYOU_ID,
    DEVICE,
    CLICK_DT,
    CLICKS,
    CAST(CASE
        WHEN
            CLICKS > 0
            AND SUM(CLICKS) OVER (PARTITION BY IPINYOU_ID,DEVICE ORDER BY CLICK_DT ROWS BETWEEN UNBOUNDED PRECEDING AND CURRENT ROW) = CLICKS
        THEN 1 -- New
        WHEN
            CLICKS > 0
            AND ( LAG(CLICKS,1,0) OVER (PARTITION BY IPINYOU_ID,DEVICE ORDER BY CLICK_DT) ) = 1
        THEN 2 -- Current Repeat Low
        WHEN
            CLICKS > 0
            AND ( LAG(CLICKS,1,0) OVER (PARTITION BY IPINYOU_ID,DEVICE ORDER BY CLICK_DT) ) > 1
        THEN 3 -- Current Repeat Hight
        WHEN
            CLICKS = 0
            AND ( SUM(CLICKS) OVER (PARTITION BY IPINYOU_ID,DEVICE ORDER BY CLICK_DT ROWS BETWEEN 3 PRECEDING AND CURRENT ROW) ) = 1
        THEN 4 -- Active Rolling 3D One Click
        WHEN
            CLICKS = 0
            AND ( SUM(CLICKS) OVER (PARTITION BY IPINYOU_ID,DEVICE ORDER BY CLICK_DT ROWS BETWEEN 3 PRECEDING AND CURRENT ROW) ) > 1
        THEN 5 -- Active Rolling 3D Repeat
        WHEN
            CLICKS = 0
            AND LAG(CLICKS,3,0) OVER (PARTITION BY IPINYOU_ID,DEVICE ORDER BY CLICK_DT) > 0
        THEN 6 -- Lapsed
        ELSE 0 -- Other
    END AS TINYINT) AS SEGMENT
FROM
    USER_ALLDATES_ALLDEVICES_CLICKS
;
DROP TABLE USER_ALLDATES_ALLDEVICES_CLICKS PURGE;

-- Calculate Customers by segments report
DROP TABLE IF EXISTS CUSTOMERS_BY_SEGMENTS PURGE;
CREATE TABLE CUSTOMERS_BY_SEGMENTS
-- CLUSTERED BY (IPINYOU_ID) INTO 32 BUCKETS
STORED AS ORC tblproperties ("orc.compress"="SNAPPY")
AS SELECT
    CLICK_DT AS DAY,
    DEVICE,
    SEGMENT,
    COUNT(IPINYOU_ID) AS CUSTOMERS_COUNT
FROM USER_DEVICE_DAY_SEGMENT
GROUP BY
    CLICK_DT,
    DEVICE,
    SEGMENT
;